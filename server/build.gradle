buildscript {
  ext {
    springBootVersion = '2.1.1.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

jacoco {
  toolVersion = "0.8.3"
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      element = 'CLASS'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
      excludes = [
          'toxiproxy.ServerApplication'
      ]
    }
  }
}

jacocoTestReport {
  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
      fileTree(dir: it,
          excludes: [
              'toxiproxy/ServerApplication*'
          ])
    })
  }
}

check.dependsOn jacocoTestCoverageVerification

group = 'toxiproxy'
sourceCompatibility = 11

repositories {
  mavenCentral()
}

dependencies {
  implementation('org.springframework.boot:spring-boot-starter-web')
  implementation('org.springframework.boot:spring-boot-starter-data-jpa')
  implementation('org.springframework.boot:spring-boot-starter-actuator')
  implementation('org.springframework.boot:spring-boot-starter-security')

  implementation('com.squareup.okhttp3:okhttp:3.14.1')
  implementation('org.flywaydb:flyway-core')
  implementation('org.apache.commons:commons-lang3:3.9')
  implementation('org.apache.commons:commons-math3:3.6.1')
  implementation('net.javacrumbs.shedlock:shedlock-spring:2.5.0')
  implementation('net.javacrumbs.shedlock:shedlock-provider-jdbc-template:2.5.0')

  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit'
  }
  testImplementation('org.mockito:mockito-core:2.27.0')
  testImplementation('org.mockito:mockito-junit-jupiter:2.27.0')
  testImplementation('org.junit.jupiter:junit-jupiter:5.4.2')
  testImplementation('net.bytebuddy:byte-buddy:1.9.12')

  runtime('com.h2database:h2')
  runtime('mysql:mysql-connector-java')
  runtimeOnly project(':frontend')
}

bootJar {
  baseName = 'server'
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}
