plugins {
  id 'org.springframework.boot' version '2.5.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
}

apply plugin: 'jacoco'

jacoco {
  toolVersion = "0.8.7"
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      element = 'CLASS'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
      excludes = [
          'toxiproxy.ServerApplication'
      ]
    }
  }
}

jacocoTestReport {
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: 'toxiproxy/ServerApplication*')
    }))
  }
}

check.dependsOn jacocoTestCoverageVerification

group = 'toxiproxy'
sourceCompatibility = 17

repositories {
  mavenCentral()
}

dependencies {
  implementation('org.springframework.boot:spring-boot-starter-web')
  implementation('org.springframework.boot:spring-boot-starter-data-jpa')
  implementation('org.springframework.boot:spring-boot-starter-actuator')
  implementation('org.springframework.boot:spring-boot-starter-security')

  implementation('com.squareup.okhttp3:okhttp:4.9.2')
  implementation('org.liquibase:liquibase-core:4.3.4')
  implementation('net.javacrumbs.shedlock:shedlock-spring:4.29.0')
  implementation('net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.29.0')

  testImplementation('org.apache.commons:commons-lang3:3.12.0')
  testImplementation('org.apache.commons:commons-math3:3.6.1')
  testImplementation('org.springframework.security:spring-security-test')
  testImplementation('org.springframework.boot:spring-boot-starter-test')

  runtimeOnly('com.h2database:h2')
  runtimeOnly('mysql:mysql-connector-java')
  runtimeOnly project(':frontend')
}

bootJar {
  archiveName 'server.jar'
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}
