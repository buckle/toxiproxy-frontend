plugins {
  id 'org.springframework.boot' version '2.5.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'org.asciidoctor.jvm.convert' version '3.3.0'
}

apply plugin: 'jacoco'

jacoco {
  toolVersion = "0.8.3"
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      element = 'CLASS'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
      excludes = [
          'toxiproxy.ServerApplication'
      ]
    }
  }
}

jacocoTestReport {
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: 'toxiproxy/ServerApplication*')
    }))
  }
}

check.dependsOn jacocoTestCoverageVerification

group = 'toxiproxy'
sourceCompatibility = 11

repositories {
  mavenCentral()
}

dependencies {
  implementation('org.springframework.boot:spring-boot-starter-web')
  implementation('org.springframework.boot:spring-boot-starter-data-jpa')
  implementation('org.springframework.boot:spring-boot-starter-actuator')
  implementation('org.springframework.boot:spring-boot-starter-security')

  implementation('com.squareup.okhttp3:okhttp:3.14.1')
  implementation('org.flywaydb:flyway-core')
  implementation('org.apache.commons:commons-lang3:3.9')
  implementation('org.apache.commons:commons-math3:3.6.1')
  implementation('net.javacrumbs.shedlock:shedlock-spring:2.5.0')
  implementation('net.javacrumbs.shedlock:shedlock-provider-jdbc-template:2.5.0')

  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit'
  }
  testImplementation('org.mockito:mockito-core:2.27.0')
  testImplementation('org.mockito:mockito-junit-jupiter:2.27.0')
  testImplementation('org.junit.jupiter:junit-jupiter:5.4.2')
  testImplementation('net.bytebuddy:byte-buddy:1.9.12')

  runtimeOnly('com.h2database:h2')
  runtimeOnly('mysql:mysql-connector-java')
  runtimeOnly project(':frontend')
}

bootJar {
  archiveName 'server.jar'
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}
